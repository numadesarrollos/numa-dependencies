apply plugin: 'kotlin-android'
apply plugin: 'kotlin-parcelize'
apply plugin: 'kotlin-kapt'
apply plugin: "de.mannodermaus.android-junit5"

android {
    compileSdkVersion rootProject.ext.sdkCompile
    buildToolsVersion rootProject.ext.buildTools

    def versionProperties = new Properties()
    file("${project.rootDir}/numa-version.properties").withInputStream { versionProperties.load(it) }

    def version = versionProperties['versionCode'].toInteger() ?: 0
    def name = versionProperties['versionName'] ?: "0.0.0"

    defaultConfig {
        minSdkVersion rootProject.ext.sdkMin
        targetSdkVersion rootProject.ext.sdkTarget
        versionCode version
        versionName name
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    lintOptions {
        abortOnError true
		disable 'NewApi' //problems lint with kotlin 1.8
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.md'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/LICENSE-notice.md'
        exclude "**/attach_hotspot_windows.dll"
        exclude "META-INF/licenses/**"
        exclude "META-INF/AL2.0"
        exclude "META-INF/LGPL2.1"
    }

    //Settings -> Build,Execution,Deployment -> Compiler -> Add "-Pfast" in "Command-line Options"
    if (project.hasProperty('fast')) {
        splits.abi.enable = false
        splits.density.enable = false
        aaptOptions.cruncherEnabled = false
    }
}

dependencies {
    implementation numaKotlin.values()

    implementation numaAndroid.androidxCore
    implementation numaAndroid.appcompat
    implementation numaAndroid.annotation

    testImplementation numaTest.values()
    testImplementation numaTestAndroid.values()
    testRuntimeOnly numaTestRuntime.values()
}

tasks.withType(Test) {
    useJUnitPlatform()
}
